set dotenv-load

# List all the tasks
list:
  @just --list --unsorted

# Initialize terraform providers
init:
  terraform init
  tflint --init

# Plan the changes
plan *FLAGS:
  terraform plan {{FLAGS}}

# Format the configuration
fmt:
  terraform fmt --recursive .

# Validate the configuration
validate:
  terraform validate .

# Lint the configuration
lint:
  tflint --module

alias f := fmt
alias v := validate
alias l := lint

# Run tests in CI
ci: check-fmt validate lint

# Check that the formatting is correct
check-fmt:
  terraform fmt --recursive --check --diff .
